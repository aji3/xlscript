buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.1'
    id 'com.jfrog.bintray' version '1.7.3'
}

apply plugin: 'java'

group = 'org.xlbean'
archivesBaseName = 'xlscript'
version = '0.1.0'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

dependencies {
	compile group: 'org.xlbean', name: 'xlbean', version:'0.3.0'
	
	compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.5.5'
    
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    
    testCompile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.14.0'
    testCompile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.8'
    testCompile group: 'com.github.tomakehurst', name: 'wiremock', version: '2.22.0'
    
    
    
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc {
    failOnError = false
    destinationDir = file('docs')
    options.locale = 'en_US'
    options.noTimestamp = true
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

jacoco {
    toolVersion = "0.7.6.201602180812"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

apply plugin: 'maven'
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://${System.env.HOME}/.m2/repository")
        }
    }
}


apply plugin: 'maven-publish'
bintray {
    user = project.hasProperty('BINTRAY_USER') ? project.getProperty('BINTRAY_USER') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('BINTRAY_API_KEY') ? project.getProperty('BINTRAY_API_KEY') : System.getenv('BINTRAY_API_KEY')
    pkg {
        repo = 'xlbean'
        name = 'xlscript'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/aji3/xlscript.git'
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = 'xlscript ' + project.version
            released  = new Date()
            vcsTag = project.version
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
            gpg {
                sign = true
                passphrase = project.hasProperty('BINTRAY_GPG_KEY') ? project.getProperty('BINTRAY_GPG_KEY') : System.getenv('BINTRAY_GPG_KEY')
            }
        }
    }
    publications = ['mavenPublication']
    configurations = ['archives']
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
        }
    }
}